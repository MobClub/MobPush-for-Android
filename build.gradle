// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven {
            url "http://mvn.mob.com/android"
        }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        // 注册MobSDK
        classpath 'com.mob.sdk:MobSDK:+'
		// 注册google services 用于集成FCM，不集成FCM可不配置
		classpath 'com.google.gms:google-services:4.0.0'
    }

    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


ext{
    compileSdkVersion = 28
    buildToolsVersion = findBuildTools()

    minSdkVersion = 16
    targetSdkVersion = 28
    versionCode = 1
    versionName = "1.0.0"
}

String findBuildTools() {
    def sdkDir = System.env.ANDROID_HOME
    if(sdkDir == null) {
        def localProperties = new File(project.rootDir, "local.properties")
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream {
                instr->properties.load(instr)
            }
            sdkDir = properties.getProperty('sdk.dir')
        }
    }
    if (sdkDir != null) {
        def buildTools = new File(sdkDir, "build-tools")
        if (buildTools.exists()) {
            def tools = buildTools.list()
            if (tools != null) {
                Arrays.sort(tools)
                return tools[tools.length - 1]
            }
        }
    }
    return "25.0.1"
}
